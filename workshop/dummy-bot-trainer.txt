settings
{
	modes
	{
		Skirmish
		{
			enabled maps
			{
				Workshop Chamber
			}
		}

		General
		{
			Limit Roles: 2 Of Each Role Per Team
		}
	}
}

variables
{
	global:
		0: bot_turn_rate
		1: bot_targeting_timeout

	player:
		0: bot_is_attacking
}

rule("Global initialize")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Global.bot_turn_rate = Workshop Setting Integer(Custom String("General"), Custom String("Bot turn rate"), 400, 200, 1000, 10);
		Global.bot_targeting_timeout = Workshop Setting Real(Custom String("General"), Custom String("Bot targeting timeout [seconds]"), 1,
			0.500, 1, 20);
	}
}

rule("Add bots")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Create Dummy Bot(Hero(Cassidy), Team 2, -1, Vector(0, 0, 0), Vector(0, 0, 0));
		Create Dummy Bot(Hero(Mei), Team 2, -1, Vector(5, 0, 5), Vector(0, 0, 0));
	}
}

rule("When bot takes damage, start facing attacker and start attacking")
{
	event
	{
		Player Took Damage;
		All;
		All;
	}

	conditions
	{
		Is Dummy Bot(Event Player) == True;
	}

	actions
	{
		Start Facing(Event Player, Direction Towards(Eye Position(Event Player), Eye Position(Attacker)), Global.bot_turn_rate, To World,
			Direction and Turn Rate);
		Wait Until(Ray Cast Hit Player(Eye Position(Event Player), Eye Position(Event Player) + Facing Direction Of(Event Player) * 100,
			Attacker, Event Player, True) != Null, Global.bot_targeting_timeout);
		Event Player.bot_is_attacking = True;
	}
}

rule("When bot is injured, start attacking")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Dummy Bot(Event Player) == True;
		Normalized Health(Event Player) <= 0.950;
		Ray Cast Hit Player(Eye Position(Event Player), Eye Position(Event Player) + Facing Direction Of(Event Player) * 100,
			All Living Players(Opposite Team Of(Team Of(Event Player))), Event Player, True) != Null;
	}

	actions
	{
		Event Player.bot_is_attacking = True;
	}
}

rule("Bot attack with primary fire")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Dummy Bot(Event Player) == True;
		Event Player.bot_is_attacking == True;
	}

	actions
	{
		If(Hero Of(Event Player) == Hero(Mei));
			Start Holding Button(Event Player, Button(Primary Fire));
		Else;
			Press Button(Event Player, Button(Primary Fire));
			Wait(0.500, Ignore Condition);
			Loop If Condition Is True;
		End;
	}
}
